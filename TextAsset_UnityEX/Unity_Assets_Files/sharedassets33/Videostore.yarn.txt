title: InitLevel_A1D2
tags: 
colorID: 2
position: -943,-1045
---
<<show VideostoreGirl>>
===
title: VideostoreGirl_00_01
tags: 
colorID: 2
position: -704,-708
---
Mae: 上夜班是吗？也不错哦。 #line:f7181e
VideostoreGirl: 我白天得在家带孩子， #line:e18ec2
VideostoreGirl: 等我妈下班回来才有时间吧？ #line:e18d25
Mae: 唉，都是因为孩子啊。 #line:1fa758
VideostoreGirl: 不过这工作也挺酷的，对吧？ #line:65e580
VideostoreGirl: 又能看电影，又能吃糖果，还能遇见帅哥，对吧？ #line:e8130c
Mae: 咦，帅哥嘛。 #line:d8fa21
VideostoreGirl: 说“帅哥"有什么问题吗？ #line:7d2c2d
Mae: 我说不清，但被称作“帅哥"的人一般都是言过其实吧。 #line:d88144
<<set $not_cute_ones to 1>>
===
title: VideostoreGirl_00_02
tags: 
colorID: 2
position: -569,-466
---
VideostoreGirl:{emote=heartbreak} #line:4458d6
<<close>>
VideostoreGirl:{emote=music} #line:4916cf
<<close>>
===
title: InitLevel_A1D3
tags: 
colorID: 2
position: -30,-1056
---
<<show Angus>>
===
title: Angus_A1D3_00
tags: 
colorID: 2
position: -134,-14
---
Angus: 嗨，梅！ #line:916eee
Angus: 你的笔记本能用了吗？ #line:a500ad
Mae: 一切正常！真是太感谢啦！ #line:d007d7
Angus: 不用谢，很高兴为您服务。 #line:465ac4
===
title: Angus_A1D3_01
tags: 
colorID: 2
position: 119,-12
---
Mae: 话说…… #line:479158
Angus: 怎么了？ #line:8ef20d
[[这年头还有人租电影看吗？|WhoRentsMoviesAnymore]] #line:a7ae9d
[[这家店是怎么一直开下去的？|HowIsThisStoreStillOpen]] #line:ec3a65
===
title: WhoRentsMoviesAnymore
tags: 
colorID: 2
position: 75,255
---
Angus: 租电影盘的人可比你想像的多哦！ #line:331e35
Angus: 比如那些什么都不懂的老年人， #line:202a2a
Angus: 还有那些因为某些原因用不了电子产品的小孩都会来这儿的。 #line:4bd6b9
Mae: 你想过要告诉他们吗？ #line:f50eee
Angus: 你指什么，是告诉他们关于网络的事吗？ #line:e1f82d
Angus: 是告诉他们可以网上订购，然后快递到家的租盘方式吗？ #line:5298b5
Mae: 是啊。 #line:f7183b
Angus: 梅，我的工作可不是为了拯救那些人。 #line:55974d
Angus: 我是别人花钱雇来站在这儿的，如果有人需要时，我就去帮他们。 #line:6cdd64
Angus: 我负责干活收钱，而不是去评判别人的所作所为。 #line:959dc7
===
title: HowIsThisStoreStillOpen
tags: 
colorID: 2
position: 329,260
---
Angus: 现在还是有不少人对DVD和录像带情有独钟呢。 #line:a9fac1
Angus: 这点可是网络没法比的。 #line:3a253e
Mae: ……真的吗？ #line:392167
Angus: 假的。 #line:6c2540
Mae: 等等，你这里真有录像带吗？ #line:991d98
Angus: 帘子后面就有录像带，大部分都是成人电影。 #line:a91e4f
Mae: 哇 哦，真的吗？ #line:46f940
Angus: 是啊，有些人就很喜欢来找这些。 #line:61fb31
Angus: 我想这是互联网之后为数不多能重温旧时光的方法吧。 #line:e376d7
Angus: 上个月就有个历史学工作者来这里寻找一些没什么人知道的录像。 #line:5b5247
Mae: 说起来你是怎么得到这份工作的？ #line:ae1a5f
Angus: 我觉得如果你是那种不太想工作的人， #line:b2ca6c
Angus: 这可能就是你能找到的唯一工作吧。 #line:434af6
===
title: Angus_A1D3_02
tags: 
colorID: 2
position: 404,-25
---
Mae: 还记得今晚的派对吧？ #line:39d06a
Angus: 当然。应该会很好玩吧？ #line:99f3ba
Mae: 我印象里你不是那种喜欢参加派对的人吧。 #line:85a142
Angus: 我确实不是那种人。不对和格雷格一起去情况就不一样了。 #line:949f12
Mae: 噢？为什么呢？ #line:ede6a3
Angus: 你知道那种有时候只想待在角落里的感觉吗？ #line:10da2a
Mae: 知道。 #line:eacca5
Angus: 格雷格就是那个能让我有地方可站的人。 #line:6d7adf
Mae: 嗷……太甜了吧…… #line:92ff49
Angus: 哈哈哈，别叫了好不好。 #line:f01675
===
title: Angus_A1D3_03
tags: 
colorID: 2
position: 654,-16
---
Mae: 过会儿见啦！ #line:bca10d
Angus: 当然！ #line:a81c47
===
title: Angus_A1D3
tags: 
colorID: 2
position: 273,-284
---
<<if $angus is 0>>
    <<set $angus += 1>>
    [[Angus_A1D3_00]]
<<elseif $angus is 1>>
    <<set $angus += 1>>
    [[Angus_A1D3_01]]
<<elseif $angus is 2>>
    <<set $angus += 1>>
    [[Angus_A1D3_02]]
<<elseif $angus is 3>>
    <<set $angus += 1>>
    <<set $angus_done to 1>>
    [[Angus_A1D3_03]]
<<endif>>
    
===
title: VideostoreGirl
tags: 
colorID: 0
position: -1094,-157
---
<<if $act is 2 and $day is 2>>
    [[VideostoreGirl_A2D2]]
<<elseif $knows_angus_at_videostore > 0>>
    <<if $videostore_girl is 0>>
        <<set $videostore_girl += 1>>
        [[VideostoreGirl_00_00]]
    <<elseif $videostore_girl is 1>>
        <<set $videostore_girl += 1>>
        [[VideostoreGirl_00_01]]
    <<else>>
        [[VideostoreGirl_00_02]]
    <<endif>>
<<else>>
    [[VideostoreGirl_CantHelp]]
<<endif>>

===
title: VideostoreGirl_00_00
tags: 
colorID: 2
position: -1043,-734
---
VideostoreGirl: 有什么能帮您的吗？ #line:bdbd4f
Mae: 安格斯在哪儿？ #line:ab4165
VideostoreGirl: 哦，他是上白班吧？ #line:ec7fd4
Mae: 现在好像就是白天吧？ #line:ff94f8
VideostoreGirl: 我好像是来早了？ #line:cd0ea2
<<if $knows_apartment_location is 0>>
	Mae: 你知道他在哪儿吗？ #line:6ecd44
	VideostoreGirl: 我猜他大概回家了吧？ #line:927bbf
	VideostoreGirl: 他好像就住在隔壁吧？ #line:6c1cbc
    VideostoreGirl: 不如你到他家按按门铃去吧？ #line:e78e65
    <<set $knows_apartment_location to 1>>
<<endif>>
Mae: 等等，为什么你每句话听起来都像是问句呢？ #line:2e8683
VideostoreGirl: 你不也一样吗？ #line:0a3398
VideostoreGirl: 我只不过是没有对此发表意见吧？ #line:0ff568
Mae: 是吗？ #line:2549ae
VideostoreGirl: 好像是吧？ #line:0be315
Mae: 嗯哼？ #line:26e987
<<set $apartment_unlocked to 1>>
===
title: VideostoreGirl_CantHelp
tags: 
colorID: 2
position: -717,76
---
VideostoreGirl: 有什么能帮您的吗？ #line:ff6184
Mae: 好像不需要…… #line:e59626
===
title: PreInitLevel
tags: 
colorID: 0
position: -183,-1518
---
<<hide Angus>>
<<hide VideostoreGirl>>
===
title: InitLevel_A2D1
tags: 
colorID: 3
position: 866,-1064
---
<<show Angus>>
===
title: InitLevel_A2D2
tags: 
colorID: 3
position: 1124,-1059
---
<<show VideostoreGirl>>
===
title: VideostoreGirl_A2D2
tags: 
colorID: 3
position: 1119,-748
---
VideostoreGirl:{emote=heartbreak} #line:ab7b35
<<close>>
VideostoreGirl:{emote=music} #line:4c5c56
<<close>>
===
title: InitLevel_A2D3
tags: 
colorID: 3
position: 1387,-1054
---
<<show Angus>>
===
title: InitLevel_A2D4
tags: 
colorID: 3
position: 1643,-1056
---
<<show Angus>>
===
title: Angus_A2D1
tags: 
colorID: 3
position: 812,-765
---
Angus: 嗨，梅。 #line:3e2005
Mae: 你好啊，安格斯。工作怎么样啊？ #line:8053c8
Angus: 你好像是今天我在店里见到的第三位客人。 #line:dcdf34
Mae: 虽然我知道你已经解释过了，但我完全不明白这家店为什么还能一直开着。 #line:12064e
Angus: 真正的原因肯定会让你吓一大跳的。 #line:5b1b34
Mae: 一会儿来乐队排练吗？ #line:5c56ea
Angus: 我得先练练嗓！ #line:bdf9a2
Mae: 你的吼功已经相当了得啦。 #line:093198
Angus: 谢谢，我天生就是个特能吼的人。 #line:44bd23
Angus: 我还是婴儿的时候就从来都不哭， #line:8210bf
Angus: 因为我只会吼。 #line:2822e8
Mae: 我9岁以前都还是个爱哭鬼呢。 #line:16fb78
Mae: 可13岁以后，我又回到以前那种哭哭啼啼的状态了…… #line:4a0ec8
Angus: 至少你还当了三年不哭的好孩子啊！ #line:7c805a
Mae: 我那三年的表现确实很不错。 #line:c408f8
Angus: 哈哈。 #line:db6048
<<set $angus_done to 1>>
===
title: InitLevel_A3D1
tags: 
colorID: 5
position: 1976,-1064
---
<<show Angus>>
===
title: Angus_A3D1
tags: 
colorID: 5
position: 1981,-799
---
Angus: 你准备好参加一会儿的乐队练习了吗？ #line:fddce9
Mae: 可能会去吧，昨天晚上真是太诡异了。 #line:5a796c
Angus: 怎么了？ #line:626638
Mae: 我等会儿告诉你。 #line:166835
<<set $angus_done to 1>>
===
title: InitLevel_A3D2
tags: 
colorID: 5
position: 2274,-1036
---
<<show Angus>>
===
title: InitLevel_A3D3
tags: 
colorID: 5
position: 2556,-1033
---
<<show Angus>>
===
title: InitLevel_A3D4
tags: 
colorID: 5
position: 2844,-1042
---
<<show Angus>>
===
title: Angus_A3D2
tags: 
colorID: 5
position: 2258,-800
---
Angus: 那么，我说过 #line:ec8502
Angus: 可以带你去公园看看。 #line:1b9e9f
Mae: 你确实说过。 #line:46adb0
Angus: 准备好出发了吗？ #line:5bbc90
->还没有。 #line:0b49e7
->是的，走吧。 #line:8fc021
    [[InvestigationQuest]]
===
title: Angus_A3D3
tags: 
colorID: 5
position: 2540,-787
---
Angus: 嗨，梅。 #line:75abda
Mae: 嗨，哥们儿。 #line:6f5563
Mae: 今天怎么样啊？ #line:e2038d
Angus: 没怎么样。 #line:e9f0a2
Mae: 你没事吧？ #line:eb6b20
Angus: 我没事。 #line:87e4b9
Mae: ……那好吧。 #line:e78413
Angus: 不好意思，别在意我。 #line:a3ba1f
<<set $angus_done to 1>>
===
title: Angus_A3D4
tags: 
colorID: 5
position: 2840,-793
---
<<if $did_angus_investigation_quest>>
    Angus: 为了甩掉那人，我腿现在还酸痛酸痛的。 #line:7e8108
    Mae: 我是哪儿都疼， #line:27c87f
    Mae: 还累得不行， #line:e0de1a
    Mae: 全身都又疼又累。 #line:993b16
    Angus: 我这辈子可没跑过这么多步。 #line:91649f
    Mae: 哈哈。 #line:a33e0d
    Mae: 我觉得我应该 #line:0f14ff
    Mae: 给你刚才的话加上句比较睿智的评论呢。 #line:659bab
    Angus: 你可以报答报答我。 #line:4dbe9c
    <<set $angus_done to 1>>
<<else>>
    Angus: 那么，我说过 #line:ea0bfd
    Angus: 我可以会带你去公园的。 #line:bf6b7d
    Mae: 你确实说过。 #line:178b0b
    Mae: 我快累死了。 #line:c024d0
    Angus: 你没事吧？ #line:93750d
    Mae: 是啊，我会没事的。 #line:6140b6
    Angus: 准备好去公园了吗？ #line:3082cc
    ->还没。 #line:290c9e
    ->准备好了。 #line:a4e818
        [[InvestigationQuest]]
<<endif>>
===
title: InvestigationQuest
tags: 
colorID: 5
position: 2501,-439
---
<<set $angus_investigation_quest to 1>>

<<if $did_gregg_friendship_quest_4>>
	Mae: 哇哦，碧娅居然同意连续两晚把车借给你开？ #line:832fed
	Angus: 是的。 #line:951300
	Mae: 就算车胎瘪了，她还同意把车借给你，是吗？ #line:5fd753
	Angus: 嗯，我想说的是，车胎瘪了的时候，你什么忙都没帮上。 #line:228165
	Angus: 再说了，新车胎也是我给换上的。 #line:39a618
	Mae: 哦，是啊。 #line:8122a4
	<<close>>
	<<wait 2>>

    <<if $did_gregg_investigation_quest>>
        Mae: 你能借到她的车，简直棒极啦。 #line:06bc67
        Mae: 我和格雷格遇见那个鬼的时候， #line:cb5b12
        Mae: 只能骑着车跑才行。 #line:e6ab10
        Angus: 我知道。格雷格得先弄个自行车驾照才行。 #line:4ea43f
        Mae: 我当时就从他车上掉下来了， #line:272e0a
        <<if $did_gregg_friendship_quest_3>>
            Mae: 当然，在那天以前，我*也*遇到了相同的事故。 #line:5acc80
        <<endif>>
    <<endif>>
<<endif>>

<<if $completed_investigation_quests is 1 and $dom_friend_bea>>
    <<set $second_iq_and_dom_friend_bea to 1>>
<<else>>
    <<set $second_iq_and_dom_friend_bea to 0>>
<<endif>>

<<if $completed_investigation_quests is 0 or $second_iq_and_dom_friend_bea>>
	Mae: 我 真 是 太 兴 奋 啦！ \o/ #line:96a59f
	Angus: 是吗？ #line:a03e2e
	Mae: 那可不！咱们还从来没单独出去过呢！ _o_ #line:2f7764
	<<if $dom_friend_gregg>>
		Angus: 嗯，好像是吧？ #line:a654a8
		Mae: 我老和格雷格一起出去玩。 #line:fdff52
		Mae: 能多了解了解你也很好嘛！ #line:9c4079
		Angus: 我这个人可好了。 #line:fa822e
	<<endif>>
	Mae: 和安格斯一起抓鬼去喽！ #line:64a1d3
<<endif>>

Angus: 你觉得你看到的那人是鬼魂吗？ #line:c914cc
Mae: 那不是人，那是个鬼！ #line:bf282a
Angus: 好吧。 #line:075335
Mae: 为什么就没人相信那是鬼吗？ #line:86e956
Angus: 我得告诉你……鬼魂什么的根本就不存在。 #line:cde003
Mae: 我很确定它们是真实存在的。 #line:5ca1b3
Angus: 额……我需要看到证据。 #line:9c5113
Mae: 怎么样才算证据呢？ #line:f3bedc
Angus: 比如，有只鬼走过来，说了句“你好"， #line:cf9b51
Angus: 然后我回应一句“嘿，我已经查明你是个死人了。"这样才行。 #line:e236cc
Mae: 那也有可能是丧尸或者是弗兰肯斯坦（科学怪人）呀。 #line:0e14ad
Angus: 好吧，它们也有可能是由鬼魂类物质组成的。 #line:b180c0
Mae: 《鬼怪那些事》可以办成一个播客诶， #line:c0e403
Mae: 话题可以是关于神啊鬼啊之类的。 #line:468308
Angus: *这集《鬼怪那些事》将带各位来到多纳狼餐厅。* #line:04b4cb
<<if $did_gregg_friendship_quest_4>>
	Mae: 算了吧，这也太逊了吧。 #line:527112
	Angus: 真抱歉。 #line:43e2cb
<<endif>>

Mae: 欢迎收看《鬼怪那些事》。 #line:5bffa2
Mae: 我是梅，我刚才看见了幽灵。|这位是安格斯，|他是个无神论者，|所以他不信我的话哦！ #line:ee2266
Angus: 我是安格斯，这位是梅。我得告诉你们，她是个疯子。 #line:5fbb76
<<irisOut 1 wait>>
<<sectionTitle AngusIQ ParkEntrance>>
===
title: Angus_A2D3
tags: 
colorID: 3
position: 1405,-752
---
Angus: 嘿，梅。 #line:927eaf
Mae: 嗨，安格斯。你的影像之国怎么样啊？ #line:c6098d
Angus: 还可以，还可以。 #line:bc92fd
Angus: 万圣节购物季又要到了， #line:c070c0
Angus: 又有一大堆不错的恐怖片上架了。 #line:3e6da0
Mae: 真是太棒啦。 #line:f2f21b
Angus: 你最喜欢哪部？ #line:53665d
Mae: 你是指恐怖片吗？ #line:1f2d4e
Angus: 没错。 #line:e813a6
    ->《萨博斯之镰》。 #line:19d027
        Angus: 哦！那部意大利恐怖片！ #line:c5003d
        Mae: 没错。意大利的恐怖片可都是用心之作，从不胡来。 #line:36009e
        Angus: 而且画面色彩也很漂亮。 #line:c46aca
        Mae: 应该说，*所有地方*都很漂亮。 #line:dbd34b
    ->《黑橡树》。 #line:9575bb
        Angus: 哦，这部电影很不错。而且还加入了女巫的元素，所以又很诡异。 #line:c407c2
        Mae: 是啊，诡异到家啦， #line:8dbfcb
        Mae: 还很吓人。 #line:d8edfd
        Angus: 是啊，这部电影 #line:54a162
        Angus: 好像还算是部历史剧。 #line:dd0465
        Mae: 只不过是女巫版的历史剧。 #line:e4d190
Mae: 好了，回见啦！ #line:12fd8a
Angus: 拜拜！ #line:fbbfc1
<<set $angus_done to 1>>
===
title: Angus_A2D4
tags: 
colorID: 3
position: 1655,-749
---
Mae: 万～圣～节～快～乐！ #line:047443
Angus: 哈哈。嘿，也祝你万圣节快乐。 #line:408f91
Mae: 你害怕吗？ #line:75ad4a
Angus: 哦，是啊，我害怕极啦。难道你没看出来吗？ #line:99e87c
Mae: 那就做个惊恐的表情。 #line:502297
<<wait 1>>
Angus: :) #line:9b8963
<<wait 2>>
Mae: :) #line:dd0651
Mae: 安格斯，你这是在笑啊。 #line:6271dc
Mae: 你这表情倒是挺可爱的，但一点也不吓人。 #line:a1ffaf
Angus: :| #line:ae1e12
Angus: 哦。 #line:9ac6b8
Mae: 但你已经努力了，干得不赖！ #line:d22323
Angus: 嘿嘿。 #line:030b3f
Mae: :| #line:02952d
<<set $angus_done to 1>>
===
